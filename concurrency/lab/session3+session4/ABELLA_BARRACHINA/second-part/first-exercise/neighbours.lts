// Two neighbor separeted by a field of berries
// They agree to permit each other to enter the field of berries but only one at a time.
// Protocol:
//  - raise a flag
//  - when (the other flag is raised) lower the flag and retrie.
//  - otherwise enter the field, pick berries, leave the field and lower the flag.
const False = 0
const True = 1
range Bool = False..True
range Card = 1..2

// alphabet extension
set BoolActions = {setTrue, setFalse, [False], [True]}
set CardActions = {set1, set2, [1], [2]}

BOOLVAR = VAL[False],
VAL[v:Bool] = 
  ( setTrue   -> VAL[True]
  | setFalse  -> VAL[False]
  | [v]       -> VAL[v]
  ).
||FLAGS = (flag1:BOOLVAR || flag2:BOOLVAR).

CARDVAR = VALC[1],
VALC[i:Card] = 
  ( set1 -> VALC[1] 
  | set2 -> VALC[2] 
  | [i]  -> VALC[i]
  ).

NEIGHBOR1 = (flag1.setTrue -> turn.set2 -> TEST),
TEST = 
  (flag2[raised:Bool] -> turn[i:Card] ->
     if (raised && i==2) 
       then (flag1.setFalse -> NEIGHBOR1) 
       else (enter -> pick -> exit -> flag1.setFalse -> NEIGHBOR1)) + {{flag1,flag2}.BoolActions, turn.CardActions}.

NEIGHBOR2 = (flag2.setTrue -> turn.set1 -> TEST2),
TEST2 = 
  (flag1[raised:Bool] -> turn[i:Card] ->
     if (raised && i==1) 
       then (flag2.setFalse -> NEIGHBOR2) 
       else (enter -> pick -> exit -> flag2.setFalse -> NEIGHBOR2)) + {{flag1,flag2}.BoolActions, turn.CardActions}.

property MUTEX = 
  ( n1.enter -> n1.pick -> n1.exit -> MUTEX 
  | n2.enter -> n2.pick -> n2.exit -> MUTEX
  ).
        
||FIELD = (n1:NEIGHBOR1 || n2:NEIGHBOR2 || {n1,n2}::FLAGS || {n1,n2}::turn:CARDVAR || MUTEX).

progress ENTER1 = {n1.enter}
progress ENTER2 = {n2.enter}

||GREEDY = FIELD << {{n1,n2}.{flag1,flag2}.setTrue}.